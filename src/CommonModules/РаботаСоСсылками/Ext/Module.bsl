// Осуществляет поиск ссылки по УИД(GUID) или массиву УИДов(GUID)
//
// Параметры:
//   УИД - Строка - Идентификатор строкой.
//       - Массив - Массив идентификаторов строкой.
//
//Возвращаемое значение:
//	- Неопределено - Если не удалось определить ни одну ссылку по параметру УИД.
//	- ЛюбаяСсылка - Если в параметр "УИД" была передана строка.
//	- ТаблицаЗначений  - Если в параметр "УИД" был передан массив строк.
//		* УИД - Строка - Содержит идентифкатор строкой по которому удалось определить ссылку.
//		* Ссылка - ЛюбаяСсылка - Содержит ссылку на объект.
//
// Пример:
// РезультатПоиска = НайтиСсылкуПоУИД("31f35109-f60a-4777-a555-f8a46c5a165d") ...
//
Функция НайтиСсылкуПоУИД(знач УИД) Экспорт
	
    ЭтоМассив = ТипЗнч(УИД) = Тип("Массив");
    
    МассивУидов = Новый Массив;
    Если ЭтоМассив Тогда
        МассивУидов = УИД;
    Иначе
        МассивУидов.Добавить(УИД);
    КонецЕсли;
    
	Результат = Неопределено;
    
    МассивМетаданных = Новый Массив;    
	МассивМетаданных.Добавить(Справочники);
	МассивМетаданных.Добавить(Документы);
	МассивМетаданных.Добавить(ПланыВидовХарактеристик);
	МассивМетаданных.Добавить(ПланыСчетов);
	МассивМетаданных.Добавить(ПланыВидовРасчета);
	МассивМетаданных.Добавить(ПланыОбмена);
	МассивМетаданных.Добавить(БизнесПроцессы);
	МассивМетаданных.Добавить(Задачи);
    
    МассивТипов = Новый Массив; 
    
    Для Каждого СтрМетаданное Из МассивМетаданных Цикл
        Для Каждого Стр Из СтрМетаданное.ТипВсеСсылки().Типы() Цикл
            МассивТипов.Добавить(Стр);
        КонецЦикла;
    КонецЦикла;
        
    ТаблицаСсылок = Новый ТаблицаЗначений;
    ТаблицаСсылок.Колонки.Добавить("УИД"    , Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(36)));
    ТаблицаСсылок.Колонки.Добавить("Ссылка" , Новый ОписаниеТипов(МассивТипов));
    
    Для Каждого СтрМетаданное Из МассивМетаданных Цикл
        Для Каждого Менеджер Из СтрМетаданное Цикл   
            Для Каждого СтрУИД Из МассивУидов Цикл
                Попытка
					obj = Менеджер.ПолучитьСсылку(Новый УникальныйИдентификатор(СтрУИД));
					Если НЕ ОбщегоНазначения.СсылкаСуществует(obj) тогда продолжить;
					КонецЕсли;	
					НовСтр = ТаблицаСсылок.Добавить();
                    НовСтр.Ссылка = obj;
                    НовСтр.УИД    = СтрУИД;
                Исключение
                КонецПопытки;
            КонецЦикла;
        КонецЦикла;
    КонецЦикла;
            
	Если ТаблицаСсылок.Количество() = 0 тогда
		Результат = неопределено;
	ИначеЕсли ТаблицаСсылок.Количество() = 1 тогда
		Результат = ТаблицаСсылок[0].Ссылка;
	Иначе
		Результат = ТаблицаСсылок;
	КонецЕсли;	
	
	Возврат Результат;
		
КонецФункции // НайтиСсылкуПоУИД 