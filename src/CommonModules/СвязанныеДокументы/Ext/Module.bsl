//С запросом
&НаСервере
Функция ПолучитьСписокСвязанныхДокументов(СсылкаНаДокумент) экспорт
    
    ТаблицаСвязанныхДокументов = Новый ТаблицаЗначений;
    ТаблицаСвязанныхДокументов.Колонки.Добавить("Ссылка");
    НоваяСтрока = ТаблицаСвязанныхДокументов.Добавить();
    НоваяСтрока.Ссылка = СсылкаНаДокумент;
    
    Запрос = Новый Запрос;
    Запрос.Текст =
    "ВЫБРАТЬ РАЗРЕШЕННЫЕ
    |    СвязанныеДокументы.Ссылка
    |ИЗ
    |    КритерийОтбора.СвязанныеДокументы(&ЗначениеКритерияОтбора) КАК СвязанныеДокументы";
    
    Для Каждого СтрокаТаблицы Из ТаблицаСвязанныхДокументов Цикл
        
        Если Метаданные.КритерииОтбора.СвязанныеДокументы.Тип.СодержитТип(ТипЗнч(СтрокаТаблицы.Ссылка))  Тогда
            
            Запрос.УстановитьПараметр("ЗначениеКритерияОтбора", СтрокаТаблицы.Ссылка);
            ТаблицаСвязанныхДокументовВременная = Запрос.Выполнить().Выгрузить();
            
            Для Каждого ЭлементКоллекции Из ТаблицаСвязанныхДокументовВременная Цикл
                РезультатПоиска = ТаблицаСвязанныхДокументов.Найти(ЭлементКоллекции.Ссылка);
                Если РезультатПоиска = НеОпределено Тогда
                    НоваяСтрока = ТаблицаСвязанныхДокументов.Добавить();
                    НоваяСтрока.Ссылка = ЭлементКоллекции.Ссылка;
                КонецЕсли;
            КонецЦикла;
        КонецЕсли;
    КонецЦикла;
        
    Возврат ТаблицаСвязанныхДокументов ;
        
КонецФункции

//Без запроса
&НаСервере
Функция ПолучитьСписокПодчиненныхДокументовНов(СсылкаНаДокумент)
    
    ТаблицаСвязанныхДокументов = Новый ТаблицаЗначений;
    ТаблицаСвязанныхДокументов.Колонки.Добавить("Ссылка");
    НоваяСтрока = ТаблицаСвязанныхДокументов.Добавить();
    НоваяСтрока.Ссылка = СсылкаНаДокумент;
    
    Для Каждого СтрокаТаблицы Из ТаблицаСвязанныхДокументов Цикл
        МассивСвязанныхДокументов = КритерииОтбора.СвязанныеДокументы.Найти(СтрокаТаблицы.Ссылка);
        
        Для Каждого ЭлементКоллекции Из МассивСвязанныхДокументов Цикл
            РезультатПоиска = ТаблицаСвязанныхДокументов.Найти(ЭлементКоллекции);
            Если РезультатПоиска = НеОпределено Тогда
                НоваяСтрока = ТаблицаСвязанныхДокументов.Добавить();
                НоваяСтрока.Ссылка = ЭлементКоллекции.Ссылка;
            КонецЕсли;
        КонецЦикла;
    КонецЦикла;
        
    Возврат ТаблицаСвязанныхДокументов ;
        
КонецФункции